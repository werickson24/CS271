// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
	
	Inc16(in = reg, out = reginc);//increment existing value
	
	Mux16(a = reg, b = reginc, sel = inc, out = incmux);//choose wether to use the incremented value or not
	Mux16(a = incmux, b = in, sel = load, out = loadmux);//choose wether to use stored/inc value or input value
	Mux16(a = loadmux, b[0..15] = false, sel = reset, out = resetmux);//choose or zero out value
	
	Register(in = resetmux, load = true, out = out, out = reg);
}
